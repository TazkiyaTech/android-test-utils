plugins {
    id("com.android.library")
    id("maven-publish")
    id("org.jetbrains.kotlin.android")
    id("signing")
}

group = getProperty("projectGroupName")
version = getProperty("projectVersionName")

android {
    compileSdk = 34
    namespace = "com.tazkiyatech.utils"

    defaultConfig {
        minSdk = 21
        targetSdk = 34
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled = false
            shrinkResources = false
        }

        release {
            minifyEnabled = false
            shrinkResources = false
        }
    }

    compileOptions {
        sourceCompatibility(JavaVersion.VERSION_17)
        targetCompatibility(JavaVersion.VERSION_17)
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    lintOptions {
        abortOnError = true
    }

    publishing {
        singleVariant("release") {
            withJavadocJar()
            withSourcesJar()
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

dependencies {
    api("androidx.test.espresso:espresso-contrib:3.5.1")
    api("androidx.test.uiautomator:uiautomator:2.2.0")
    implementation("androidx.test:core:1.5.0")

    testImplementation(libs.junit.jupiter.api)
    testImplementation(libs.junit.jupiter.engine)
    testImplementation(libs.junit.jupiter.params)

    androidTestImplementation("androidx.test.ext:junit:1.1.5")
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId getProperty("projectGroupName")
                artifactId getProperty("projectArtifactName")
                version getProperty("projectVersionName")

                pom {
                    name = getProperty("projectArtifactName")
                    description = "An Android library containing classes and methods designed to ease the writing of Android instrumentation tests."
                    url = "https://github.com/TazkiyaTech/android-test-utils"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    organization {
                        name = "Tazkiya Tech"
                        url = "http://tazkiyatech.com"
                    }
                    developers {
                        developer {
                            id = "adil-hussain-84"
                            name = "Adil Hussain"
                            email = "adilson05uk@gmail.com"
                        }
                    }
                    scm {
                        connection = "scm:git:git://github.com/TazkiyaTech/android-test-utils.git"
                        developerConnection = "scm:git:ssh://github.com:TazkiyaTech/android-test-utils.git"
                        url = "https://github.com/TazkiyaTech/android-test-utils"
                    }
                }
            }
        }

        repositories {
            maven {
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username getProperty("sonatype_username")
                    password getProperty("sonatype_password")
                }
            }
        }
    }

    signing {
        // the 'signing.keyId', 'signing.password' and 'signing.secretKeyRingFile' properties required by this task are defined outside of this project in the '~/.gradle/gradle.properties' file
        sign publishing.publications.release
    }
}
